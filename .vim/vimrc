" Settings                                                              {{{
" --------------------------------------------------------------------- {|}

set nocompatible
if has("autocmd")
    filetype plugin indent on
endif

if has("syntax") && !exists("syntax_on")
    syntax enable
endif

set number relativenumber linebreak backspace=indent,eol,start
set mouse=a ttymouse=sgr mousehide clipboard=unnamed
set ttyfast lazyredraw autoread confirm hidden
set ignorecase smartcase autoindent smartindent formatoptions+=l
set nomodeline nostartofline t_vb= nrformats-=octal
set softtabstop=4 shiftwidth=4 shiftround smarttab expandtab
set wildmenu wildmode=list:longest,full
set complete-=i completeopt=noselect,menuone,menuone
set laststatus=2 showtabline=2 display+=lastline
set splitbelow splitright scrolloff=8 sidescrolloff=8
set backupdir=$HOME/.vim/backups noswapfile
set sessionoptions-=options viewoptions-=options
set path-=/usr/include path+=** define= include=
set foldmethod=indent foldlevel=99 foldclose=all
set wildignore=*~,#*#,*.7z,.DS_Store,.git,.hg,.svn,
    \*.a,*.adf,*.asc,*.au,*.aup,*.avi,*.bin,*.bmp,*.bz2,
    \*.class,*.db,*.dbm,*.djvu,*.docx,*.exe,*.filepart,*.flac,*.gd2,
    \*.gif,*.gifv,*.gmo,*.gpg,*.gz,*.hdf,*.ico,*.iso,*.jar,*.jpeg,*.jpg,
    \*.m4a,*.mid,*.mp3,*.mp4,*.o,*.odp,*.ods,*.odt,*.ogg,*.ogv,*.opus,
    \*.pbm,*.pdf,*.png,*.ppt,*.psd,*.pyc,*.rar,*.rm
    \,*.s3m,*.sdbm,*.sqlite,*.swf,*.swp,*.tar,*.tga,*.ttf,*.wav,*.webm,
    \*.xbm,*.xcf,*.xls,*.xlsx,*.xpm,*.xz,*.zip,
    \*/node_modules/*,*/dist/*,*/build/*

if filereadable("/usr/share/dict/words")
    set dictionary=/usr/share/dict/words
endif

" don't backup some system files for security
if v:version > 801 || v:version == 801 && has("patch1519")
    set backupskip&
endif
set backupskip+=/dev/shm/*,/usr/tmp/*,/var/tmp/*,*/systemd/user/*

if &ttimeoutlen == -1
    set ttimeout ttimeoutlen=100
endif

if has("extra_search")
    set hlsearch incsearch
endif

if has("multi_byte_encoding")
    set listchars+=extends:»,precedes:«
    set listchars+=multispace:·\ ,trail:·
    " set listchars+=nbsp:␣,eol:⮠
    let &showbreak= "…  "
    set fillchars+=diff:╱
else
    set listchars+=extends:>,precedes:<
    let &showbreak= "... "
endif

if exists("+breakindent")
    set breakindent
endif

if has("syntax")
    set cursorline
    set colorcolumn=+2
endif

if exists("+wildignorecase")
    set wildignorecase
endif

if exists("+spelloptions")
    set spelloptions+=camel
endif

if has("cmdline_hist") && &history < 1000
    set history=1000
endif

if &tabpagemax < 50
    set tabpagemax=50
endif

if !empty(&viminfo)
    set viminfo^=!
endif

if has("virtualedit")
    set virtualedit+=block
endif

if v:version > 703 || v:version == 703 && has("patch541")
    set formatoptions+=j
endif

if has("persistent_undo")
    set undodir=$HOME/.vim/undos
    set undofile
endif

" --------------------------------------------------------------------- }}}
" Keymaps                                                               {{{
" --------------------------------------------------------------------- {|}

if has("keymap")
    let mapleader = " "
    let maplocalleader = "\\"

    "" general keymaps                                            {{{

    nnoremap <leader>q :q<cr>
    nnoremap <leader>w :w<cr>

    nnoremap Y y$
    vnoremap Y y

    nnoremap n nzzzv
    nnoremap N Nzzzv

    nnoremap <C-u> <C-u>zz
    nnoremap <C-d> <C-d>zz

    " move line(s) up and down
    nnoremap <S-Down> <CMD>m .+1<CR>==
    nnoremap <S-Up> <CMD>m .-2<CR>==
    inoremap <S-Down> <esc><CMD>m .+1<CR>==gi
    inoremap <S-Up> <esc><CMD>m .-2<CR>==gi
    vnoremap <S-Down> :m '>+1<CR>gv=gv
    vnoremap <S-Up> :m '<-2<CR>gv=gv

    nnoremap ]<space> <CMD>call append(line('.'), repeat([''], v:count1))<CR>
    nnoremap [<space> <CMD>call append(line('.') - 1, repeat([''], v:count1))<CR>

    xnoremap g/ <esc>/\\%V
    nnoremap <expr> <silent> gV "`[" . strpart(getregtype(), 0, 1) . "`]"

    if empty(mapcheck("<C-U>", "i"))
        inoremap <C-U> <C-G>u<C-U>
    endif

    if empty(mapcheck("<C-W>", "i"))
        inoremap <C-W> <C-G>u<C-W>
    endif

    "" https://vi.stackexchange.com/a/213
    " move down jumping over blank lines and indents
    nnoremap <silent> gJ :let _=&lazyredraw<CR>
                \ :set lazyredraw<CR>/\%<C-R>=virtcol(".")
                \ <CR>v\S<CR>:nohl<CR>:let &lazyredraw=_<CR>

    " move up jumping over blank lines and indents
    nnoremap <silent> gK :let _=&lazyredraw<CR>
                \ :set lazyredraw<CR>?\%<C-R>=virtcol(".")
                \ <CR>v\S<CR>:nohl<CR>:let &lazyredraw=_<CR>

    "" :W sudo saves the file
    command! W execute "w !sudo tee % > /dev/null" <bar> edit!

    nnoremap cd :<C-U>cd %:h <Bar> pwd<CR>

    "" - - - - - - - - - - - - - - - - - - - - - - - - - - - - -  }}}
    "" misc input/terminal/command mode keymaps                   {{{
    inoremap , ,<C-g>u
    inoremap ? ?<C-g>u
    inoremap . .<C-g>u
    inoremap ; ;<C-g>u

    tnoremap <leader><Esc> <C-\><C-n>
    tnoremap <Esc><Esc> <C-\><C-n>
    tnoremap <S-Space> <Space>
    tnoremap <C-Space> <Space>

    cnoremap <expr> <c-n> wildmenumode() ? "\<c-n>" : "\<down>"
    cnoremap <expr> <c-p> wildmenumode() ? "\<c-p>" : "\<up>"


    "" - - - - - - - - - - - - - - - - - - - - - - - - - - - - -  }}}
    "" git difftool/mergetool keymaps for selecting hunks         {{{
    nnoremap <leader>gw :diffget <bar> diffupdate<CR>
    vnoremap <leader>gw :diffget <bar> diffupdate<CR>
    nnoremap <leader>gr :diffget<CR>
    vnoremap <leader>gr :diffget<CR>

    nnoremap <leader>mu :diffupdate<CR>
    nnoremap <leader>mb :diffget BA <bar> diffupdate<CR>
    nnoremap <leader>mB :%diffget BA <bar> diffupdate<CR>

    " LOCAL is the left buffer when using vimdiff
    nnoremap <leader>mh :diffget LO <bar> diffupdate<CR>
    nnoremap <leader>mH :%diffget LO <bar> diffupdate<CR>

    " REMOTE is the rightmost buffer
    nnoremap <leader>ml :diffget RE <bar> diffupdate<CR>
    nnoremap <leader>mL :%diffget RE <bar> diffupdate<CR>


    "" - - - - - - - - - - - - - - - - - - - - - - - - - - - - -  }}}
    "" lists - next/prev                                          {{{

    "" Argument list
    nnoremap [a :previous<CR>
    nnoremap ]a :next<CR>
    nnoremap [A :last<CR>
    nnoremap ]A :first<CR>

    "" Buffer list
    nnoremap [b :bprevious<CR>
    nnoremap ]b :bnext<CR>
    nnoremap [B :blast<CR>
    nnoremap ]B :bfirst<CR>

    "" Quickfix list
    nnoremap [q :cprevious<CR>
    nnoremap ]q :cnext<CR>
    nnoremap [Q :clast<CR>
    nnoremap ]Q :cfirst<CR>

    "" Location list
    nnoremap [l :lprevious<CR>
    nnoremap ]l :lnext<CR>
    nnoremap [L :llast<CR>
    nnoremap ]L :lfirst<CR>

    "" Tab list
    nnoremap [t :tabprevious<CR>
    nnoremap ]t :tabnext<CR>
    nnoremap [T :tlast<CR>
    nnoremap ]T :tfirst<CR>

    "" - - - - - - - - - - - - - - - - - - - - - - - - - - - - -  }}}
    "" buffers, tabs, and windows                                 {{{

    "" close buffer
    nnoremap <leader>x :bdelete<CR>

    "" close all the buffers
    nnoremap <leader>xa :bufdo bdelete<CR>

    "" picks buffer
    nnoremap <leader>bj :<C-U>buffers<CR>:buffer<Space>

    "" Managing tabs
    nnoremap <leader>tn :tabnew<CR>
    nnoremap <leader>to :tabonly<CR>
    nnoremap <leader>tc :tabclose<CR>

    "" toggles between this and the last accessed tab
    let s:last_tab = 1
    nnoremap <leader>tl :exe "tabn ".g:lasttab<CR>
    au TabLeave * let s:last_tab = tabpagenr()

    " Create splits
    nnoremap <leader>- :split<cr>
    nnoremap <leader>\ :vsplit<cr>

    " Navigate splits
    nnoremap <C-h> <C-W>h
    nnoremap <C-j> <C-W>j
    nnoremap <C-k> <C-W>k
    nnoremap <C-l> <C-W>l

    " Resize splits
    nnoremap <Left> :vertical resize -5<CR>
    nnoremap <Down> :resize -5<CR>
    nnoremap <Up> :resize +5<CR>
    nnoremap <Right> :vertical resize +5<CR>

    "" - - - - - - - - - - - - - - - - - - - - - - - - - - - - -  }}}
    "" toggle options                                             {{{

    "" toggles highlighted cursor row; doesn't work in visual mode
    nnoremap <leader>sx <CMD>set cursorline!<CR>

    "" toggles highlighted cursor column; works in visual mode
    noremap <leader>sy <CMD>set cursorcolumn!<CR>

    "" toggles highlighting search results
    nnoremap <leader>sh <CMD>set hlsearch!<CR>

    "" toggles showing matches as I enter my pattern
    nnoremap <leader>si <CMD>set incsearch!<CR>

    "" toggles spell checking
    nnoremap <leader>ss <CMD>set spell!<CR>

    "" toggles paste
    nnoremap <leader>sp <CMD>set paste!<CR>

    "" toggles showing tab, end-of-line, and trailing white space
    noremap <leader>sl <CMD>set list!<CR>

    "" toggles line number display
    noremap <leader>sn <CMD>set relativenumber!<CR>

    "" toggles soft wrapping
    noremap <leader>sw <CMD>set wrap!<CR>

    "" toggle colorcolumn
    nnoremap <silent> <leader>s\| <CMD>execute "set colorcolumn="
                    \ . (&colorcolumn == "" ? "80" : "")<CR>

    "" toggle foldcolumn
    nnoremap <silent> <leader>sf <CMD>execute "set foldcolumn="
                    \ . (&foldcolumn == "0" ? "1" : "0")<CR>

    "" toggle system clipboard
    nnoremap <silent> <leader>sc <CMD>execute "set clipboard="
                    \ . (&clipboard == "umnamed"
                        \ ? "unnamed,unnamedplus"
                        \ : "unnamed")<CR>

    "" - - - - - - - - - - - - - - - - - - - - - - - - - - - - -  }}}
    "" plugins                                                    {{{

    if isdirectory(expand("$HOME/dev/lib/fzf"))
        set runtimepath+=~/dev/lib/fzf
        nnoremap <C-p> :Files<CR>
        nnoremap <C-g> :GFiles<CR>
        nnoremap <leader>ff :Files<CR>
        nnoremap <leader>fg :GFiles<CR>
        nnoremap <leader>fb :Buffers<CR>
        nnoremap <leader>fd :LS<CR>
    endif

    nnoremap <leader>u <CMD>packadd undotree<CR><cmd>UndotreeToggle<cr>

    nnoremap - <CMD>packadd vifm.vim<CR><cmd>Vifm<cr>

    nnoremap <leader>gg <CMD>packadd vim-fugitive<CR><cmd>G<cr>
    nnoremap <leader>gc <CMD>packadd vim-fugitive<CR><cmd>G commit<cr>
    nnoremap <leader>gb <CMD>packadd vim-fugitive<CR><cmd>G blame<cr>
    nnoremap <leader>gd <CMD>packadd vim-fugitive<CR><cmd>Gdiffsplit<cr>
    nnoremap <leader>gl <CMD>packadd vim-fugitive<CR><cmd>0Gclog --follow<cr>
    xnoremap <leader>gl :Gclog --follow<CR>

    nnoremap <leader>gD <CMD>packadd vim-fugitive<CR><cmd>G difftool -y<cr>
    nnoremap <leader>gM <CMD>packadd vim-fugitive<CR><cmd>G mergetool -y<cr>

    nnoremap <leader>gW <CMD>packadd vim-fugitive<CR><cmd>Gwrite<cr>
    nnoremap <leader>gR <CMD>packadd vim-fugitive<CR><cmd>Gread<cr>

    "" - - - - - - - - - - - - - - - - - - - - - - - - - - - - -  }}}
endif

" --------------------------------------------------------------------- }}}
" Autocommands                                                          {{{
" --------------------------------------------------------------------- {|}

if has("autocmd")
    augroup jamin_vimrc
        autocmd!

        "" setup syntax complete for filetypes that don't have their own
        if exists("+omnifunc")
            autocmd Filetype *
              \ if &omnifunc == "" |
              \    setlocal omnifunc=syntaxcomplete#Complete |
              \ endif
        endif

        autocmd BufNew,BufEnter * let g:git_branch = g:GitBranch()

        autocmd InsertEnter,WinLeave * setlocal nocursorline
        autocmd InsertLeave,WinEnter *
                    \ if !exists("g:vscode") && !exists("b:no_cursorline") || !b:no_cursorline
                    \ |   setlocal cursorline
                    \ | endif
    augroup END
endif

" --------------------------------------------------------------------- }}}
" Functions and user commands                                           {{{
" --------------------------------------------------------------------- {|}

if has("eval")
    command! Src :source $MYVIMRC

    "" get the git branch for the current buffer                  {{{
    function! GitBranch()
        try
            if exists("*fugitive#Head") && FugitiveGitDir() != expand("~")."/.git"
                return fugitive#Head()
            else
                let branch = systemlist(
                            \   "git -C " .
                            \   shellescape(expand("%:p:h")) .
                            \   " branch --show-current"
                            \ )[0]

                return v:shell_error ? "" : branch
            endif
        catch
            return ""
        endtry
    endfunction

    "" - - - - - - - - - - - - - - - - - - - - - - - - - - - - -  }}}
    "" toggle the display of a terminal buffer                    {{{
    let s:term_buf_nr = -1
    function! s:TerminalToggle() abort
        if s:term_buf_nr == -1
            execute "botright terminal"
            let s:term_buf_nr = bufnr("$")
        else
            try
                execute "bdelete! " . s:term_buf_nr
            catch
                let s:term_buf_nr = -1
                call <SID>TerminalToggle()
                return
            endtry
            let s:term_buf_nr = -1
        endif
    endfunction

    command! TerminalToggle call <sid>TerminalToggle()
    nnoremap <silent> <leader>T <CMD>TerminalToggle<CR>
    tnoremap <silent> <leader>T <C-w>N:TerminalToggle<CR>

    "" - - - - - - - - - - - - - - - - - - - - - - - - - - - - -  }}}
endif

" --------------------------------------------------------------------- }}}
" Statusline                                                            {{{
" --------------------------------------------------------------------- {|}

let g:use_devicons = expand("$USE_DEVICONS") != 0
        \ && expand("$USE_DEVICONS") == 1
        \ || expand("$TERM") == "wezterm" ||
        \ system('tmux showenv | grep -c "TERM=wezterm"') > 0

if has("eval") && has("statusline")

    let g:git_branch="" " gets set in an autocmd above
    function! DisplayGitBranch()
        let icon = g:use_devicons ? "   " : "  "
        return g:git_branch != "" ? icon .. g:git_branch .. "  " : ""
    endfunction

    set statusline=%#TabLineSel#                    " TabLineSel highlight
    set statusline+=\ \                             " Whitespace
    set statusline+=[%n]                            " Buffer number
    set statusline+=%m                              " Modified flag
    set statusline+=%r                              " Readonly flag
    set statusline+=%h                              " Help file flag
    set statusline+=%w                              " Preview window flag
    set statusline+=%q                              " Quickfix/location list flag
    set statusline+=%y                              " File type
    set statusline+=\ \                             " Whitespace
    set statusline+=%#TabLine#                      " TabLine highlight
    set statusline+=%{g:DisplayGitBranch()}         " Git branch name
    set statusline+=%#TabLineFill#                  " TabLineFill highlight
    set statusline+=%=                              " Left/Right separator
    set statusline+=\ \                             " Whitespace
    set statusline+=%#TabLine#                      " TabLine highlight
    set statusline+=\ \                             " Whitespace
    set statusline+=%f                              " File name
    set statusline+=\ \                             " Whitespace
    set statusline+=%#TabLineSel#                   " TabLineSel highlight
    set statusline+=\ \                             " Whitespace
    set statusline+=%c:[%l/%L]                      " cursor:line/total_lines
    set statusline+=\ \                             " Whitespace
endif

"----------------------------------------------------------------------}}}
" Terminal options                                                      {{{
"-----------------------------------------------------------------------{|}

" :help terminal-output-codes

" Fix modern terminal features
" https://sw.kovidgoyal.net/kitty/faq/#using-a-color-theme-with-a-background-color-does-not-work-well-in-vim
if !has('gui_running') && &term =~ '^\%(screen\|tmux\|xterm\|wezterm\)'

    " Styled and colored underline support
    let &t_AU = "\e[58:5:%dm"
    let &t_8u = "\e[58:2:%lu:%lu:%lum"
    let &t_Us = "\e[4:2m"
    let &t_Cs = "\e[4:3m"
    let &t_ds = "\e[4:4m"
    let &t_Ds = "\e[4:5m"
    let &t_Ce = "\e[4:0m"

    " Strikethrough
    let &t_Ts = "\e[9m"
    let &t_Te = "\e[29m"

    " Enable true colors, see  :help xterm-true-color
    let &t_8f = "\e[38:2:%lu:%lu:%lum"
    let &t_8b = "\e[48:2:%lu:%lu:%lum"
    let &t_RF = "\e]10;?\e\\"
    let &t_RB = "\e]11;?\e\\"

    " Enable bracketed paste mode, see  :help xterm-bracketed-paste
    let &t_BE = "\e[?2004h"
    let &t_BD = "\e[?2004l"
    let &t_PS = "\e[200~"
    let &t_PE = "\e[201~"

    " Cursor control
    if exists("+cursorshape")
        let &t_RS = "\eP$q q\e\\"
        let &t_RC = "\e[?12$p"
        let &t_VS = "\e[?12l"
        let &t_SH = "\e[%d q"
        let &t_SI = "\e[6 q"
        let &t_SR = "\e[4 q"
        let &t_EI = "\e[2 q"
    endif

    " Enable focus event tracking, see  :help xterm-focus-event
    let &t_fe = "\e[?1004h"
    let &t_fd = "\e[?1004l"
    execute "set <FocusGained>=\e[I"
    execute "set <FocusLost>=\e[O"

    " Window title
    let &t_ST = "\e[22;2t"
    let &t_RT = "\e[23;2t"

    " Enable modified arrow keys, see  :help arrow_modifiers
    execute "silent! set <xUp>=\e[@;*A"
    execute "silent! set <xDown>=\e[@;*B"
    execute "silent! set <xRight>=\e[@;*C"
    execute "silent! set <xLeft>=\e[@;*D"

    " vim hardcodes background color erase even if the terminfo file does
    " not contain bce. This causes incorrect background rendering when
    " using a color theme with a background color in terminals such as
    " kitty that do not support background color erase.
    let &t_ut=""

endif

" --------------------------------------------------------------------- }}}
" Colorscheme                                                           {{{
" --------------------------------------------------------------------- {|}

if has("eval") && has("syntax")

    set background=dark
    set t_Co=256

    if exists("+termguicolors")
        set termguicolors
    endif

    if v:version < 500
        finish
    endif

    packadd! gruvbox-material

    function! s:gruvbox_material_custom() abort
        let s:palette = gruvbox_material#get_palette("soft", "material", {
                    \"bg_orange": ["#5A3B0A", "130"],
                    \ "bg_visual_yellow": ["#7a380b", "208"]
                    \ })

        call gruvbox_material#highlight("DiffDelete", s:palette.bg5, s:palette.bg_diff_red)
        call gruvbox_material#highlight("DiffChange", s:palette.none, s:palette.bg_orange)
        call gruvbox_material#highlight("DiffText", s:palette.fg0, s:palette.bg_visual_yellow)
        highlight! link CursorLineNr Purple
    endfunction

    augroup GruvboxMaterialCustom
        autocmd!
        autocmd ColorScheme gruvbox-material call s:gruvbox_material_custom()
    augroup END

    let g:gruvbox_material_background = "soft"
    let g:gruvbox_material_foreground = "original"
    let g:gruvbox_material_ui_contrast = "high"
    let g:gruvbox_material_statusline_style = "material"
    let g:gruvbox_material_diagnostic_virtual_text = "colored"
    " let g:gruvbox_material_spell_foreground = "colored"
    " let g:gruvbox_material_sign_column_background = "grey"
    " let g:gruvbox_material_menu_selection_background = "orange"
    " let g:gruvbox_material_current_word = "bold"
    " let g:gruvbox_material_visual = "reverse"

    let g:gruvbox_material_better_performance = 1
    let g:gruvbox_material_diagnostic_text_highlight = 0
    let g:gruvbox_material_transparent_background = 1
    let g:gruvbox_material_disable_italic_comment = 1
    " let g:gruvbox_material_enable_italic = 1
    " let g:gruvbox_material_enable_bold = 1
    " let g:gruvbox_material_disable_terminal_colors = 1
    " let g:gruvbox_material_dim_inactive_windows = 1

    colorscheme gruvbox-material
endif

"----------------------------------------------------------------------}}}
